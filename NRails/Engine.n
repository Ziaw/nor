using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Reflection;
using System.Collections.Generic;
using System.Linq;
using Rsdn.Janus;
using Rsdn.Janus.Mssql;
using System.IO;
using System.Web;

namespace NRails
{
  /// <summary>
  /// Description of Engine.
  /// </summary>
  public class Engine
  {
      [Accessor]
      cfg : Configuration;
      
      public this(cfg : Configuration)
      {
          this.cfg = cfg;
      }
      
      public GetSchema() : DBSchema
      {
          GetSchema(cfg.Env);
      }
      
      mutable cachedSchema : DBSchema;
      public GetSchema(env : string) : DBSchema
      {
          when (cachedSchema == null)
          {
            def connStr = cfg.GetConnectionString(env).ConnectionString;
            // todo: вынести в фабрику
            def db = MssqlDriver();
            def schemaDriver = db.CreateSchemaDriver();
            cachedSchema = schemaDriver.LoadExistingSchema(connStr);
          }

          cachedSchema;
      }

      public GetTable(typeName : string) : TableSchema
      {
          def sch = GetSchema();
          
          sch.Tables.FirstOrDefault(t => t.Name == typeName);
      }
      
      mutable static instance : Engine;
      
      public static Instance : Engine 
      { 
        get 
        { 
          when (instance == null)
          {
            instance = MakeRunTimeInstance();
          }
          instance;
        }
      }
      
      public static CompileTimeInstance(prjPath : string) : Engine 
      { 
        when (instance == null)
        {
          instance = MakeCompileTimeInstance(FileInfo(prjPath).DirectoryName);
        }
        instance;
      }
      
      static compileTimeConfigLocation(locations : list[string]) : string
      {
         def defaultConfigSearch = ["../../nrails.cfg", "nrails.cfg"];
         
         locations
            .SelectMany(_ => defaultConfigSearch, (l, f) => (l, f))
            .Map((path, file) => Path.Combine(path, file))
            .FirstOrDefault(f => File.Exists(f));
      }
      
      static MakeRunTimeInstance() : Engine
      {
         def location = match (HttpContext.Current == null)
         {
           | true => compileTimeConfigLocation([
              Environment.CurrentDirectory, 
              Assembly.GetCallingAssembly().Location, 
              Assembly.GetExecutingAssembly().Location].Distinct().ToList());
           | false => HttpContext.Current.Server.MapPath("~/nrails.cfg");
         } 

         when (location == null)
               throw FileNotFoundException("nrails.cfg not found");
               
         Engine(Configuration(location))
      }

      static MakeCompileTimeInstance(prjPath : string) : Engine
      {
         def location = match (HttpContext.Current == null)
         {
           | true => compileTimeConfigLocation([prjPath]);
           | false => HttpContext.Current.Server.MapPath("~/nrails.cfg");
         } 

         when (location == null)
               throw FileNotFoundException($"nrails.cfg not found in $prjPath");
               
         Engine(Configuration(location))
      }
  }
}
