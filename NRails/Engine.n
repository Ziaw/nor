using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Reflection;
using System.Collections.Generic;
using System.Linq;
using Rsdn.Janus;
using Rsdn.Janus.Mssql;
using System.IO;
using System.Web;

namespace NRails
{
  /// <summary>
  /// Description of Engine.
  /// </summary>
  public class Engine
  {
      [Accessor]
      cfg : Configuration;
      
      public this(cfg : Configuration)
      {
          this.cfg = cfg;
      }
      
      public GetSchema() : DBSchema
      {
          GetSchema(cfg.Env);
      }
      
      mutable cachedSchema : DBSchema;
      public GetSchema(env : string) : DBSchema
      {
          when (cachedSchema == null)
          {
            def connStr = cfg.GetConnectionString(env).ConnectionString;
            // todo: вынести в фабрику
            def db = MssqlDriver();
            def schemaDriver = db.CreateSchemaDriver();
            cachedSchema = schemaDriver.LoadExistingSchema(connStr);
          }

          cachedSchema;
      }

      public GetTable(typeName : string) : TableSchema
      {
          def sch = GetSchema();
          
          sch.Tables.FirstOrDefault(t => t.Name == typeName);
      }
      
      static instance : Engine;
      
      public static Instance : Engine 
      { 
        get 
        { 
          instance;
        }
      }
      
      static defaultConfigSearch : list[string] = ["../../nrails.cfg", "nrails.cfg"];
      static compileTimeConfigLocation() : string
      {
         def locations = [
              Environment.CurrentDirectory, 
              Assembly.GetCallingAssembly().Location, 
              Assembly.GetExecutingAssembly().Location,
         ].Distinct();
         
         def paths = locations
            .SelectMany(_ => defaultConfigSearch, (l, f) => (l, f))
            .Map((path, file) => Path.Combine(path, file));

         paths.FirstOrDefault(f => File.Exists(f));
      }
      
      static this()
      {
         def location = match (HttpContext.Current == null)
         {
           | true => compileTimeConfigLocation();
           | false => HttpContext.Current.Server.MapPath("~/nrails.cfg");
         } 

         when (location == null)
               throw FileNotFoundException("nrails.cfg not found");
               
         unless (String.IsNullOrEmpty(location))
            instance = Engine(Configuration(location))
      }
  }
}
