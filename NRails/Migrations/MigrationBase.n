using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using SCG = System.Collections.Generic;
using System.Linq;
using Nemerle.Assertions;
using Nemerle.Extensions;
using NRails.Database.Schema;

namespace NRails.Migrations
{
  public variant MigrationAction
  {
      | CreateTable { table : TableSchema; actions : list[TableAction] }
      | ChangeTable { table : TableSchema; actions : list[TableAction] }
      | RenameTable { table : TableSchema; newName : string }
      | DropTable { table : TableSchema }
  }
  
  public variant TableAction
  {
      | AddColumn { col : TableColumnSchema }
      | ChanedColumn { col : TableColumnSchema; newCol : TableColumnSchema}
      | RemoveColumn { colSchema : TableColumnSchema }
  }
  
  public interface IActionHolder[T]
  {
      Add(action : T) : void;
      GetActions() : list[T];
  }
  
  public class ActionHolder[T] : IActionHolder[T]
  {
      mutable actionStack : list[T] = [];

      protected AddAction(action : T) : void implements IActionHolder[T].Add
      {
          actionStack = action :: actionStack;
      }
      
      GetActions() : list[T] implements IActionHolder[T].GetActions
      {
          actionStack.Rev();
      }
  }
  
  public class TableActions : ActionHolder[TableAction]
  {
      #region Helpers
      [Accessor]
      tableSchema : TableSchema;
      
      private GetColumn([NotNull]name : string) : TableColumnSchema
      {
          def col = tableSchema.GetColumn(name);
          when (col == null) throw ArgumentException($"Column '$name' not found in table $(tableSchema.Name)", "name");
          col;          
      }

      public this([NotNull]tableSchema : TableSchema)
      {
          this.tableSchema = tableSchema;
      }
      #endregion
      
      public Add([NotNull] colAction : TableColumnSchema -> void) : void
      {
          def colSchema = TableColumnSchema();
          
          colAction(colSchema);
          
          AddAction(TableAction.AddColumn(colSchema));
      }

      public Change([NotNull] name : string, [NotNull] colAction : TableColumnSchema -> void) : void
      {
          def oldColumn = GetColumn(name);
          
          def newColumn = oldColumn.Clone();
          
          colAction(newColumn);
          
          AddAction(TableAction.ChanedColumn(oldColumn, newColumn));
      }

      public Remove([NotNull] name : string) : void
      {
          AddAction(TableAction.RemoveColumn(GetColumn(name)));
      }
  }
  
  public interface IMigration : IActionHolder[MigrationAction]
  {
      AssignSchema(schema : DBSchema) : void;
  }
  
  public class MigrationBase : ActionHolder[MigrationAction], IMigration
  {
      mutable schema : DBSchema;

      #region Helpers
      private GetTable([NotNull]name : string) : TableSchema
      {
          AssertSchema();
          
          def table = schema.GetTable(name);
         
          when (table == null) throw ArgumentException($"Table '$name' not found", "name");
          
          table;
      }
      
      private RunActions([NotNull]table : TableSchema, [NotNull]action : TableActions -> void) 
         : (TableSchema * list[TableAction])
      {
          def actions = TableActions(table);

          action(actions);
          
          (table, (actions : IActionHolder[TableAction]).GetActions())
      }
      
      private AssertSchema() : void
      {
          when (schema == null) throw InvalidOperationException("Schema not set");
      }
      #endregion
      
      #region IMigration
      _AssignSchema([NotNull] schema : DBSchema) : void implements IMigration.AssignSchema
      {
          this.schema = schema;
      }
      #endregion
      
      protected CreateTable([NotNull] name : string, [NotNull] action : TableActions -> void) : void
      {
          def table = TableSchema();
          table.Name = name;
          
          AddAction(MigrationAction.CreateTable(RunActions(table, action)));
      }

      protected ChangeTable([NotNull] name : string, [NotNull] action : TableActions -> void) : void
      {
          AddAction(MigrationAction.ChangeTable(RunActions(GetTable(name), action)));
      }

      protected RenameTable([NotNull] name : string, [NotNull] newName : string) : void
      {
          AddAction(MigrationAction.RenameTable(GetTable(name), newName));
      }
      
      protected DropTable([NotNull] name : string) : void
      {
          AddAction(MigrationAction.DropTable(GetTable(name)));
      }
   }
}
