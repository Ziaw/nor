using Nemerle;
using Nemerle.Collections;
using Nemerle.Compiler;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle.Compiler.Parsetree;

using System;
using System.Text;
using System.Collections.Generic;
using System.Linq;
using NRails;
using NRails.Database.Schema;

namespace NRails.Macros.Model
{
    [Nemerle.MacroUsage (Nemerle.MacroPhase.BeforeInheritance, Nemerle.MacroTargets.Class,
                     Inherited = false)]
    public macro Model(t : TypeBuilder)
    {
        ModelImpl.RegisterModelType(t);
    }

    [Nemerle.MacroUsage (Nemerle.MacroPhase.WithTypedMembers, Nemerle.MacroTargets.Class,
                     Inherited = false)]
    public macro Model(t : TypeBuilder)
    {
        ModelImpl.GenerateColumns(t);
    }

  
  internal module ModelImpl
  {
      public RegisterModelType(t : TypeBuilder) : void
      {
          DatabaseManagerImpl.RegisterModelType(t.Name, t.FullName);
      }
      
      public GenerateColumns(t : TypeBuilder) : void
      {
          def engine = try
          {
            Engine.CompileTimeInstance(t.Manager.Options.ProjectPath);
          }
          catch
          {
            | e => Message.Error($"Engine initialization error $e"); throw;
          }
          try 
          {
            def table = engine.GetTable(t.Name);
            
            when (table == null)
            {
               def msg = $"table $(t.Name) not found, env: '$(engine.Cfg.Env)'";
               throw Exception(msg);
            }
            
            t.GetModifiers().AddCustomAttribute(<[  BLToolkit.DataAccess.TableName($(table.Name : string)) ]>);

            def existingProperties = t.GetProperties().Map(p => p.Name);            
            
            def hintBuilder = StringBuilder();
            
            foreach (col : TableColumnSchema in table.Columns)
            {
                // todo : проверка должна идти по атрибутам тулкита, 
                // а не по именам свойств
                unless (existingProperties.Any(p => p == col.Name))
                {
                    def clrType = col.Type.Type;
                    def propType = PExpr.FromQualifiedIdentifier(t.Manager, clrType.FullName);
                    def fieldMemberName = $"_$(col.Name)";
                   
                    t.Define(<[decl: 
                        private mutable $(fieldMemberName : dyn) : $propType; 
                    ]>);
                    
                    t.Define(<[decl: 
                        [BLToolkit.Mapping.MapField($(col.Name : string))] 
                        public $(col.Name : dyn ) : $propType
                        {
                            get { $(fieldMemberName : dyn); }
                            set { $(fieldMemberName : dyn) = value; }
                        }
                    ]>);
                    _ = hintBuilder.Append($"$(col.Name) : $(clrType.Name)\n");
                }
            }
            
            //unless (hintBuilder.Length == 0)
            //    Message.Hint("\n" + hintBuilder.ToString())
          }
          catch 
          {
             | e => Message.Error($"$(t.Name) model error: $(e.ToString())");
          }
      }
  }
}
